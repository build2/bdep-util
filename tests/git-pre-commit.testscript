# file      : tests/git-pre-commit.testscript
# license   : MIT; see accompanying LICENSE file

.include git-common.testscript git-pre-commit-common.testscript

: version-check
:
{
  $clone_prj;

  cat <<EOI >=prj/manifest;
    : 1
    name: foo
    version: 1.2.3
    summary: Foo
    EOI
  touch prj/TODO;
  $ga .;
  $gc;                       # No committed manifest.

  echo '@@' >=prj/TODO;

  $gc 2>>EOE != 0            # Package with a final version is changed.
    error: changing released package foo 1.2.3 without version increment
      info: use --no-verify git option to suppress
    EOE
}

: copyright-check
:
{
  $clone_prj;

  cat <<EOI >=prj/LICENSE;
    Copyright (c) 2014-2019 the build2 authors.
    EOI
  $ga .;

  # Note that the 'WARNING:' prefix is printed in bold red.
  #
  $gc 2>>~%EOE%
    %.+WARNING:.+ last copyright year in 'LICENSE' is 2019%
    EOE
}
